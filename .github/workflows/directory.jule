use std::process::{exit}
use std::fs::{DirEntry, FsError, OFlag, File, Directory}

static mut TABLE: str = ""

static IGNORE_PACKAGES: [...]str = [
    ".",
    "..",
    ".github",
    ".git",
]

fn workflow_failed(message: str) {
    const FAIL_EXIT_CODE = 1
    outln(message)
    exit(FAIL_EXIT_CODE)
}

fn write(text: str) { TABLE += text }
fn write_line(text: str) { write(text + "\n") }

fn sort_dirents(mut &dirents: []&DirEntry) {
    let less = fn(s1: str, s2: str): bool {
        let s1r = ([]rune)(s1)
        let s2r = ([]rune)(s2)

        let mut n = s1r.len
        if n > s2r.len {
            n = s2r.len
        }

        let mut i = 0
        for i < n; i++ {
            let ir = s1r[i]
            let jr = s2r[i]

            if ir != jr {
                ret ir < jr
            }
        }

        ret s1r.len < s2r.len
    }

    let mut i = 0
    for i < dirents.len-1; i++ {
        let mut j = 0
        for j < dirents.len-i-1; j++ {
            if less(dirents[j+1].name, dirents[j].name) {
                dirents[j+1], dirents[j] = dirents[j], dirents[j+1]
            }
        }
    }
}

fn make_name(mut s: str): str {
    // Remove all cutset substrings.
    let cutset_subs = [
        ".jule",
        ".",
        "/",
        "\\",
    ]
    for _, subs in cutset_subs {
        s = s.replace(subs, "", -1)
    }

    // Replace all underlines with space.
    s = s.replace("_", " ", -1)

    // Make uppercase first character of all words.
    let mut i = 0
    let mut wait_space = false
    for i < s.len; i++ {
        let b = s[i]
        if wait_space {
            if b == ' ' {
                wait_space = false
            }
            continue
        }

        s[i] = 'Z' - ('z' - b)

        wait_space = true
    }

    ret s
}

fn append_package(package: str) {
    let mut dirents = Directory.read(package) else {
        workflow_failed("package did not readed: " + package)
        ret // Avoid error.
    }

    let package_table_name = make_name(package)

    write_line("")
    write_line("")
    write("## ")
    write(package_table_name)
    sort_dirents(dirents)
    for _, dirent in dirents {
        // Ignore sub-diretories.
        if dirent.stat.is_dir() || dirent.name.has_suffix("_test.jule") {
            continue
        }

        let file_table_name = make_name(dirent.name)
        let file_url = package + "/" + dirent.name

        write_line("")
        write("  - [")
        write(file_table_name)
        write("](")
        write(file_url)
        write(")")
    }
}

fn create_directory_md() {
    const MD_PATH = "./DIRECTORY.md"
    File.write(MD_PATH, []byte(TABLE), 0o660) else {
        workflow_failed("a problem occurs when creating " + MD_PATH)
    }
}

fn main() {
    write_line("# Table of Contents")

    let mut dirents = Directory.read(".") else {
        workflow_failed("directory did not readed")
        ret // Avoid error.
    }

    sort_dirents(dirents)

dirent:
    for _, d in dirents {
        for _, ip in IGNORE_PACKAGES {
            if ip == d.name {
                continue dirent
            }
        }

        if d.stat.is_dir() {
            append_package(d.name)
        }
    }

    create_directory_md()
}
